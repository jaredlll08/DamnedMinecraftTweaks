buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = "https://repo.spongepowered.org/maven" }
        flatDir {
            dirs 'lib'
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'maven-publish'

version = '1.0.5'
group = 'com.blamejared.damnedminecrafttweaks'
archivesBaseName = 'DamnedMinecraftTweaks-1.16.5'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    mappings channel: 'official', version: '1.16.5'
    runs {
        client {
            arg "-mixin.config=damnedminecrafttweaks.mixins.json"
            workingDirectory project.file('run')
            mods {
                damnedminecrafttweaks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run_server')
            mods {
                damnedminecrafttweaks {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            args '--mod', 'damnedminecrafttweaks', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                damnedminecrafttweaks {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    flatDir {
        dirs "lib"
    }
    maven {
        url 'https://maven.blamejared.com'
        content {
            includeGroup "com.blamejared.crafttweaker"
        }
    }

}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.8'

    implementation fg.deobf("curse.maven:scaling-health-248027:3348269")
    implementation fg.deobf("curse.maven:silent-lib-242998:3385768")

    implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.16.5:7.1.2.454")


    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
    implementation fg.deobf(fileTree('lib') { include '*.jar' })
    //Cursemaven wasn't working properly with Mine And Slash so I just did this

}
mixin {

    add sourceSets.main, 'damnedminecrafttweaks.refmap.json'

}
jar {
    manifest {
        attributes([
                "Specification-Title"     : "damnedminecrafttweaks",
                "Specification-Vendor"    : "BlameJared",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "BlameJared",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "damnedminecrafttweaks.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

publish.dependsOn(project.tasks.getByName("assemble"))
publish.mustRunAfter(project.tasks.getByName("build"))

publishing {

    publications {

        mavenJava(MavenPublication) {

            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {

        maven {

            url "file://" + System.getenv("local_maven")
        }
    }
}